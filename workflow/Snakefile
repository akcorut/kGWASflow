# =================================================================================================

from snakemake.utils import min_version

# Make Sure Minimun Snakemake version
min_version("7.25.0")

# =========================================================================================================
#     Setup Config and Report
# =========================================================================================================

# Load config file
configfile: "config/config.yaml"

# validate(config, schema="../schemas/config.schema.yaml")

# Description of the workflow can be found in the final report
report: "report/workflow.rst"

container: "docker://continuumio/miniconda3"

# =========================================================================================================
#     Load Rules
# =========================================================================================================

include: "rules/common.smk"     
include: "rules/qc.smk"
include: "rules/trimming.smk"
include: "rules/build_kmers_gwas.smk"
include: "rules/count_kmers.smk"
include: "rules/combine_and_filter.smk"
include: "rules/generate_kmers_table.smk"
include: "rules/generate_kinship_matrix.smk"
include: "rules/kmers_gwas.smk"
include: "rules/summarize_results.smk"
include: "rules/fetch_kmers.smk"
include: "rules/filter_kmers.smk"
include: "rules/ref.smk"
include: "rules/align_kmers.smk"
include: "rules/fetch_reads.smk"
include: "rules/align_reads.smk"
include: "rules/assemble_reads.smk"
include: "rules/align_contigs.smk"
# Activate igv_report if only igv_report is active in config.yaml
if config["settings"]["assemble_reads"]["igv_report"] or config["settings"]["align_reads_with_kmers"]["igv_report"]:
    include: "rules/igv_report.smk"
include: "rules/blast_contigs.smk"

# =========================================================================================================
#     The `onstart` Checker
# =========================================================================================================

onstart:
    try:
        print("Checking if all required files are provided...")
        important_files = [ config["samples"],
                            config["phenotypes"] ]
        for filename in important_files:
            if not os.path.exists(filename):
                raise FileNotFoundError(filename)
    except FileNotFoundError as e:
        print("This file is not available or accessible: %s" % e)
        sys.exit(1)
    else:
        print("\tAll required files are present!")


# =========================================================================================================
#     Target Outputs
# =========================================================================================================

rule all:
    input:
        get_target_output,

# =========================================================================================================
#     Success and Failure Messages
# =========================================================================================================

onsuccess:
	print("Success! The Snakemake workflow is completed.")

onerror:
	print("Error! The Snakemake workflow aborted.")